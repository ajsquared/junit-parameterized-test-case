buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group 'com.andrewjamesjohnson'
version '1.0.2-SNAPSHOT'
archivesBaseName = "junit-parameterized-test-case"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
// If these properties aren't set, use a dummy value to prevent failures
ext.ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : 'dummy'
ext.ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : 'dummy'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'junit-parameterized-test-case'
                packaging 'jar'
                description 'Library for parameterizing individual test methods in JUnit'
                url 'https://github.com/ajsquared/junit-parameterized-test-case'

                scm {
                    connection 'https://github.com/ajsquared/junit-parameterized-test-case.git'
                    developerConnection 'git@github.com:ajsquared/junit-parameterized-test-case.git'
                    url 'https://github.com/ajsquared/junit-parameterized-test-case/'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/ajsquared/junit-parameterized-test-case/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'ajsquared'
                        name 'Andrew Johnson'
                        email 'andrew@andrewjamesjohnson.com'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = 'com.andrewjamesjohnson'
}
